buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
        maven { url = 'https://maven.parchmentmc.org' }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/repository/maven-public'
        }
        maven {
            name = "TimeConqueror's Maven"
            url = "https://repo.repsy.io/mvn/timeconqueror/mc/"
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.27-patched'
        classpath 'org.parchmentmc:librarian:1.+'
        classpath 'org.spongepowered:mixingradle:0.7.+'
        classpath "gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.6.10"
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'com.github.johnrengelman.shadow'

apply from: 'gradle/scripts/configs.gradle'
apply from: 'gradle/scripts/version_handler.gradle'
apply from: 'gradle/scripts/updater.gradle'

version = versionGetter.get()
group = 'ru.timeconqueror'
archivesBaseName = 'TimeCore'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

mixin {
    add sourceSets.main, "mixins.timecore.refmap.json"
    config "mixins.timecore.json"

//    addManualRefmap("src/main/resources/manual_refmap.json")

    debug = true
    debug.verbose = true
    debug.countInjections = true
    debug.export = true
    checks.interfaces = true
}

repositories {
    flatDir {
        dirs 'runtimeMods'
    }
    mavenCentral()
    mavenLocal()
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/repository/maven-public'
    }
    maven {
        name = "TimeConqueror's Maven"
        url = "https://repo.repsy.io/mvn/timeconqueror/mc/"
    }
}

// Allows to 3rd libraries be added to dev workspace
minecraft.runs.all {
    lazyToken('minecraft_classpath') {
        configurations.library.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
    }
}

minecraft {
    mappings channel: 'parchment', version: '1-1.18.2'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    def extraArgs = []
    def extraVmArgs = []
    runs {
        client {
            workingDirectory project.file('run')

//            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            setArgs(extraArgs)
            setJvmArgs(extraVmArgs)

            mods {
                timecore {
                    source sourceSets.main
                    source sourceSets.test

                    resource sourceSets.main.resources
                    resource sourceSets.test.resources
                }
            }
        }

        server {
            workingDirectory project.file('run')
            //property 'forge.logging.markers', 'REGISTRIES'

            //property 'forge.logging.console.level', 'debug'

            setArgs(extraArgs)
            setJvmArgs(extraVmArgs)

            mods {
                timecore {
                    source sourceSets.main
                    source sourceSets.test

                    resource sourceSets.main.resources
                    resource sourceSets.test.resources
                }
            }
        }

        data {
            workingDirectory project.file('run')

//            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            args '--mod', 'timecore', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            setArgs(extraArgs)
            setJvmArgs(extraVmArgs)

            mods {
                timecore {
                    source sourceSets.main
                    source sourceSets.test

                    resource sourceSets.main.resources
                    resource sourceSets.test.resources
                }
            }
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    // configuration that holds jars to include in the jar

    //library - 3rd-party library (not a mod)
    //shade - dep that should be shaded
    implementation.extendsFrom(library)
    library.extendsFrom(shade)
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

dependencies {
    minecraft 'net.minecraftforge:forge:1.18.2-40.0.2'

    if (file("runtimeMods").exists()) {
        file("runtimeMods").eachFile { file ->
            def splitPos = file.name.lastIndexOf("-")
            def modartifact = file.name.substring(0, splitPos)
            def modversion = file.name.substring(splitPos + 1, file.name.length() - 4)
            def modreference = "lib:$modartifact:$modversion"

            dependencies {
                implementation fg.deobf(project.dependencies.create(modreference) {
                    transitive = false
                })
            }
        }
    }

    groovy.lang.Closure withoutAnnotations = { exclude group: 'org.jetbrains', module: 'annotations' }
    shade "org.jetbrains.kotlin:kotlin-stdlib-jdk8", withoutAnnotations
    shade "org.jetbrains.kotlin:kotlin-reflect", withoutAnnotations

    if (System.getProperty("idea.sync.active") != "true") {
        annotationProcessor("org.spongepowered:mixin:0.8.5:processor")
    }
}

shadowJar {
    configurations = [project.configurations.shade]

    def shadowPackages = [
            'kotlin',
            'org.intellij',
            'org.jetbrains'
    ]

    shadowPackages.forEach {
        relocate(it, "tcrepack.$it")
    }

    mergeServiceFiles()
    exclude '**/module-info.class'
}

reobf {
    shadowJar {}
    jar {}
}

tasks.build.dependsOn reobfShadowJar
jar.finalizedBy('reobfShadowJar')

jar {
    from sourceSets.main.output
    exclude '**/module-info.class'

    from { configurations.shade.collect { it.isDirectory() ? it : zipTree(it) } }

    manifest {
        attributes([
                "Specification-Title"     : "timecore",
                "Specification-Vendor"    : "Time_Conqueror",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "Time_Conqueror",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        ])
    }

    finalizedBy('reobfJar')
}

task devJar(type: Jar) {
    from sourceSets.main.output
    exclude '**/module-info.class'

    it.archiveClassifier = 'dev'

    manifest {
        attributes([
                "Specification-Title"     : "timecore",
                "Specification-Vendor"    : "Time_Conqueror",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : archiveVersion,
                "Implementation-Vendor"   : "Time_Conqueror",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        ])
    }

    finalizedBy('reobfDevJar')
}
reobf.create("devJar")

// Create source attachment for dev jar
task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    it.archiveClassifier = 'sources'
}


processResources {
    exclude 'manual_refmap.json'
}

def inProjectMaven = "${project.projectDir.absolutePath.replace("\\", "/")}/maven"

publishing {
    publications {
        mavenJava(MavenPublication) { p ->

            artifact jar
            artifact devJar
            artifact sourcesJar

//            pom.withXml {
//                def dependenciesNode = asNode().appendNode('dependencies')
//
//                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
//                configurations.shade.allDependencies.each {
//                    def dependencyNode = dependenciesNode.appendNode('dependency')
//                    dependencyNode.appendNode('groupId', it.group)
//                    dependencyNode.appendNode('artifactId', it.name)
//                    dependencyNode.appendNode('version', it.version)
//                }
//            }
        }
    }
    repositories {
        maven {
            url inProjectMaven
        }
        if (secretProps != null) {
            maven {
                url "https://repo.repsy.io/mvn/${secretProps.repsy.user}/${secretProps.repsy.repo_name}"
                credentials {
                    username secretProps.repsy.login
                    password secretProps.repsy.password
                }
            }
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '17'
        languageVersion = '1.6'
        freeCompilerArgs = Arrays.asList("-Xjvm-default=all", "-Xopt-in=kotlin.RequiresOptIn")
    }
}

task printVersion {
    println("Compiled version: $version")
}

publish.finalizedBy(printVersion)
