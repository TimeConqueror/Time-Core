if (!project.properties["timecore.version"]) {
    throw new IllegalStateException("gradle.properties should contain property 'timecore.version=<VERSION_PLACEHOLDER>' with defined timecore version.")
}

buildscript {
    repositories {
        mavenCentral()
        maven { url = 'https://maven.minecraftforge.net/' }
        maven {
            name = "TimeConqueror's Maven"
            url = "https://repo.repsy.io/mvn/timeconqueror/mc/"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+'
        classpath "gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0"

    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.github.johnrengelman.shadow'

configurations {
    //library - 3rd-party library (not a mod)
    implementation.extendsFrom(library)
}

// Allows to 3rd libraries be added to dev workspace
minecraft.runs.all {
    lazyToken('minecraft_classpath') {
        configurations.library.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
    }
}

dependencies {
    library 'org.jetbrains:annotations:23.1.0'

    groovy.lang.Closure withoutAnnotations = { exclude group: 'org.jetbrains', module: 'annotations' }
    library "org.jetbrains.kotlin:kotlin-stdlib-jdk8", withoutAnnotations
    library "org.jetbrains.kotlin:kotlin-reflect", withoutAnnotations

    implementation fg.deobf("ru.timeconqueror:TimeCore:${project.properties["timecore.version"]}:dev")
}

shadowJar {
    archiveClassifier.set('')
    configurations = []

    def shadowPackages = [
            'kotlin',
            'org.intellij',
            'org.jetbrains'
    ]

    shadowPackages.forEach {
        relocate(it, "tcrepack.$it")
    }
    finalizedBy('reobfShadowJar')
}

reobf {
    jar {}
    shadowJar {}
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = Arrays.asList("-Xjvm-default=all", "-Xopt-in=kotlin.RequiresOptIn")
    }
}